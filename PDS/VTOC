         TITLE ' - READ FORMAT1 DSCB AND SETUP DCB AND SPACE INFO.'
* THIS CODE USES OBTAIN TO READ IN THE FORMAT 1 DSCB AND A FORMAT 3
* DSCB IF ONE EXISTS. INFORMATION IS EXTRACTED AND PLACED INTO THE
* FIELDS: LRECL, BLKSIZE AND TRACKS.
VTOC     $PROLOG R12
         L     R4,=V(DATASECT)                                     @130
         USING DATASECT,R4                                         @130
         XC    BLKSIZE,BLKSIZE     RESET BLKSIZE
         XC    LRECL,LRECL         RESET LRECL
         XC    TRACKS,TRACKS       RESET TRACKS.
         XC    EXTENTS,EXTENTS     RESET EXTENTS
*        XC    CREDT,CREDT         RESET CREATE DATE
*        XC    EXPDT,EXPDT         RESET EXPIRATION DATE
*        XC    REFDT,REFDT         RESET REFERENCED DATE
         MVC   CAMVOL,L1VOLUME     USE VOLSER FROM BCS VOLCELL
         CLC   CAMVOL,=C'MIGRAT'   IS IT MIGRATED?
         BE    VTOC99              IF SO, WHY BOTHER.
*        MVC   CAMDSN,L1DSNAME     USE DSNAME FROM BCS.
         MVC   CAMDSN,COMPNAME     USE DSNAME FROM BCS.
         OBTAIN  SEARCH            READ FORMAT 1 DSCB BY DSNAME.
         ST    R15,OBTAINRC        SAVE OBTAIN RETURN CODE.
         LTR   R15,R15             WAS IT THERE?
         BZ    VTOC1               YES.
         MVC   TRACKS,=XL4'FFFFFFFF' INDICATE DATASET NOT FOUND 30NOV87
         CLC   DSPFX(3),=C'   '    WAS A DATASET PREFIX GIVEN   15JAN88
         BE    VTOC1A           NO,CONTINUE AS NORMAL           15JAN88
         L     R9,DSPFXL           LOAD LENGTH OF PREFIX        15JAN88
         EX    R9,COMPDSNV         COMPARE DSNAME TO PREFIX        @134
         BNE   VTOC99         YES, DON'T ADD TO COUNT           15JAN88
VTOC1A   AP    CNTNF,ONE           OTHERWISE COUNT AN ORPHAN.
        $MESSAGE 7,DATA=COMPNAME                                   @130
         MVI   L1DSNAME+45,C'*'    HILITE DATASET IN LISTING.   30NOV87
         B     VTOC99              AND LEAVE IT BLANK.
VTOC1    SR    R1,R1               CLEAR REGISTER 1.
         ICM   R1,3,DS1BLKL        PICK UP BLKSIZE
         ST    R1,BLKSIZE          AND SAVE FOR PRINT OUT.
         ICM   R1,7,DS1SCAL3       PICK UP SECONDARY SPACE.        @130
         CVD   R1,WORK             CONVERT TO DECIMAL.             @130
         $EDITP  L1ALSEC,                                          @130X
               DWK3,ZZZZ9          FORMAT IT.                      @130
         TM    DS1SCAL1,DS1CYL     CYLINDER ALLOCATION
         BM    VTOC1Z              NO, DO TRACK CALCULATION
         SR    R0,R0               CLEAR A REGISTER.
         SR    R1,R1               CLEAR A REGISTER.
         LA    R5,DS1EXT1          ADDRESS PRIMARY EXTENT
         ICM   R0,3,2(R5)          STARTING CC
         ICM   R1,3,6(R5)          ENDING CC
         SR    R1,R0               CYLS IN THIS EXTENT IN R1
         LA    R1,1(,R1)           KEEP IT HONEST
         B     VTOC1C
VTOC1Z   EQU   *
         SR    R0,R0               CLEAR A REGISTER.
         LA    R5,DS1EXT1          ADDRESS PRIMARY EXTENT
         ICM   R0,3,2(R5)          STARTING CC
         SR    R14,R14             CLEAR A REGISTER.
         L     R14,TRKCYL          TRACKS PER CYLINDER
         SRDA  R0,32               SETUP FOR MULTIPLY.
         MR    R0,R14              R1 = TRKS IN FULL CYLINDERS.
         ICM   R14,3,4(R5)         STARTING TT
         AR    R1,R14              STARTING RELATIVE TRACK.
         ST    R1,STARTTRK         SAVE IT.
         SR    R0,R0               CLEAR A REGISTER.
         ICM   R0,3,6(R5)          ENDING CC
         SR    R14,R14             CLEAR A REGISTER.
         L     R14,TRKCYL          TRACKS PER CYLINDER
         SRDA  R0,32               SETUP FOR MULTIPLY.
         MR    R0,R14              R1 = TRKS IN FULL CYLINDERS.
         ICM   R14,3,8(R5)         ENDING TT
         AR    R1,R14              STARTING RELATIVE TRACK.
         S     R1,STARTTRK         TRACKS IN THIS EXTENT IN R1
         LA    R1,1(,R1)           KEEP IT HONEST
VTOC1C   EQU   *
         CVD   R1,WORK             CONVERT TO DECIMAL.             @130
         $EDITP  L1ALPRM,                                          @130X
               DWK3,ZZZZ9          FORMAT IT.                      @130
         TM    DS1SCAL1,DS1CYL     CYLINDER ALLOCATION
         BM    VTOC1D
         MVC   L1ALTYP,=C'CYL'
         B     VTOC1E
VTOC1D   EQU   *
         TM    DS1SCAL1,DS1TRK     TRACK ALLOCATION
         BZ    VTOC1Y
         MVC   L1ALTYP,=C'TRK'
         B     VTOC1E
VTOC1Y   TM    DS1SCAL1,DS1AVR     BLOCK ALLOCATION
         BZ    VTOC1E
         MVC   L1ALTYP,=C'BLK'
VTOC1E   EQU   *
         SR    R1,R1               CLEAR REGISTER                  @130
         ICM   R1,3,DS1RKP         PICK UP KEY POSITION
         CVD   R1,WORK             CONVERT TO DECIMAL
         $EDITP  L1KPOS,                                           @130X
               DWK2,ZZZ            FORMAT IT NICELY.
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,DS1KEYL        PICK UP KEY LENGTH.
         CVD   R1,WORK             CONVERT TO DECIMAL
         $EDITP  L1KLEN,                                           @130X
               DWK2,ZZZ            FORMAT IT NICELY.
         ICM   R1,3,DS1LRECL       PICK UP LRECL
         ST    R1,LRECL            AND SAVE FOR PRINT OUT.
         L     R5,DSEXTAD          ADDRESS EXTENT TABLE
         MVC   0(30,R5),DS1EXT1 SAVE EXTENTS 1 TO 3
         CLC   DS1PTRDS,=5X'00'    IS THERE A FORMAT 3?
         BE    VTOC2A              NO, SKIP IT.
         LA    R5,30(R5)           GO AROUND FIRST 3 EXTENTS
         MVC   CCHHR3,DS1PTRDS     POINTER TO FORMAT 3 DSCB.
VTOC1B   OBTAIN  F3CAMLST          READ FORMAT 3 BY DISK ADDRESS.
         LTR   R15,R15             DID WE GET IT?
         BNZ   VTOC2A              OH WELL
         MVC   0(40,R5),DS3EXTNT  EXTENTS 4,5,6 AND 7.
         LA    R5,40(R5)
         MVC   0(90,R5),DS3ADEXT EXTENTS 8 TO 16.
         LA    R5,90(R5)
         CLC   DS3PTRDS,=5X'00'    IS THERE A FORMAT 3?
         BE    VTOC2A              NO, SKIP IT.
         MVC   CCHHR3,DS3PTRDS     POINTER TO FORMAT 3 DSCB.
         B     VTOC1B
VTOC2A   SR    R15,R15             ZERO LOOP REGISTER
         SR    R8,R8               ZERO COUNT OF TRACKS ALLOCATED.
         IC    R15,DS1NOEPV        GET NUMBER OF EXTENTS
         LTR   R15,R15             ARE THERE ANY EXTENTS?
         BZ    VTOC5A              SKIP IT IF ZERO EXTENT
         STCM  R15,15,EXTENTS
         L     R5,DSEXTAD          START AT THE BEGINNING
VTOC5B   EQU   *
         CLI   0(R5),X'00'         IS THIS AN ACTIVE EXTENT?
         BE    VTOC5C              IF NOT, SKIP IT.
         SR    R0,R0               CLEAR A REGISTER.
         ICM   R0,3,2(R5)          STARTING CC
         SR    R14,R14             CLEAR A REGISTER.
         L     R14,TRKCYL          TRACKS PER CYLINDER
         SRDA  R0,32               SETUP FOR MULTIPLY.
         MR    R0,R14              R1 = TRKS IN FULL CYLINDERS.
         ICM   R14,3,4(R5)         STARTING TT
         AR    R1,R14              STARTING RELATIVE TRACK.
         ST    R1,STARTTRK         SAVE IT.
         SR    R0,R0               CLEAR A REGISTER.
         ICM   R0,3,6(R5)          ENDING CC
         SR    R14,R14             CLEAR A REGISTER.
         L     R14,TRKCYL          TRACKS PER CYLINDER
         SRDA  R0,32               SETUP FOR MULTIPLY.
         MR    R0,R14              R1 = TRKS IN FULL CYLINDERS.
         ICM   R14,3,8(R5)         ENDING TT
         AR    R1,R14              STARTING RELATIVE TRACK.
         S     R1,STARTTRK         TRACKS IN THIS EXTENT IN R1
         LA    R1,1(,R1)           KEEP IT HONEST
         AR    R8,R1               COUNT IT
VTOC5C   LA    R5,10(R5)           GET AN EXTENT ADDR IN R5.
         BCT   R15,VTOC5B          LOOP THROUGH ALL EXTENTS.
VTOC5A   LA    R8,0(0,R8)          PURIFY A REGISTER.
         STCM  R8,15,TRACKS        TRACKS ALLOCATED
         SLR   R1,R1
         IC    R1,DS1CREDT
         CVD   R1,DW
         LH    R1,DW+6
         SRL   R1,4
         SLL   R1,16
         SLR   R0,R0
         ICM   R0,3,DS1CREDT+1
         CVD   R0,DW
         LH    R0,DW+6
         OR    R0,R1
         ST    R0,WORKA
         MVC   CREDT,WORKA+1
*        MVC   CREDT,DS1CREDT
         SLR   R1,R1
         IC    R1,DS1EXPDT
         CVD   R1,DW
         LH    R1,DW+6
         SRL   R1,4
         SLL   R1,16
         SLR   R0,R0
         ICM   R0,3,DS1EXPDT+1
         CVD   R0,DW
         LH    R0,DW+6
         OR    R0,R1
         ST    R0,WORKA
         MVC   EXPDT,WORKA+1
*        MVC   EXPDT,DS1EXPDT
         SLR   R1,R1
         IC    R1,DS1REFD
         CVD   R1,DW
         LH    R1,DW+6
         SRL   R1,4
         SLL   R1,16
         SLR   R0,R0
         ICM   R0,3,DS1REFD+1
         CVD   R0,DW
         LH    R0,DW+6
         OR    R0,R1
         ST    R0,WORKA
         MVC   REFDT,WORKA+1
*        MVC   REFDT,DS1REFD
VTOC99   DS    0H
         $EPILOG ,                 ALL DONE.
         LTORG ,
COMPDSNV CLC   DSPFX(0),COMPNAME   EXECUTED COMMAND                @134
         SPACE ,
SEARCH   CAMLST  SEARCH,CAMDSN,CAMVOL,DS1FMTID
CAMDSN   DC    CL44' '
CAMVOL   DC    CL6' '
DW       DC    D'0'
WORKA    DC    F'0'
FORMAT1  EQU   *
         IECSDSL1 (1)
OBTAINRC DC    F'0'
F3CAMLST CAMLST  SEEK,CCHHR3,CAMVOL,FORMAT3
CCHHR3   DC    XL5'00'
FORMAT3  EQU   *
         IECSDSL1 (3)
