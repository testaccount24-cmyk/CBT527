         TITLE ' - FINDVVDS. ADVANCE R6 TO A SPECIFIED VVDS CELL.'
* I ENTER HERE WITH R6 POINTING TO A CELL I HAVE JUST PROCESSED. THIS
* CODE EITHER MOVES AHEAD TO THE NEXT CELL BE IT WHATEVER OR SEARCHES
* FOR A PARTICULAR CELL TYPE.
* ON ENTRY THE NEXT-TO-RIGHT-MOST BYTE OF VVD0 CONTAINS X'FF' IF THE
* CELL TYPE THE CALLER WANTS IS OPTIONAL (IT'S EITHER THE NEXT ONE OR
* IT'S NOT THERE AT ALL) OR X'00' IF THE DESIRED CELL TYPE IS REQUIRED
* BUT MAY NOT BE THE NEXT ONE.
* THE RIGHT-MOST BYTE OF VVD0 CONTAINS THE ID OF THE CELL TYPE THE
* CALLER WANTS.
* IF VVD0 IS ALL ZEROS THE CALLER JUST WANTS THE NEXT CELL.
* IF THE REQUESTED CELL TYPE WAS FOUND R0 IS CLEARED.
FINDVVDS $PROLOG R12
         L     R4,=V(DATASECT)                                     @130
         USING DATASECT,R4                                         @130
         L     R0,VVD0             PICK UP CALLERS PARM            @134
         ST    R6,VVD6             SAVE CELL ADDRESS
FINDVVD1 SR    R1,R1               CLEAR REGISTER
         SR    R15,R15             CLEAR PREVIOUS RETCODE.         @122
         ICM   R1,3,0(R6)          PICK UP LENGTH OF CURRENT CELL.
         LTR   R1,R1               IS THERE A LENGTH?              @132
         BNP   FINDVVDX            NO. HOW ODD!                    @132
         AR    R6,R1               SKIP OVER LENGTH
         C     R6,ENDVVD           ANY MORE CELLS IN THIS RECORD?
         BL    FINDVVD2            YES.
         ICM   R8,15,VVDSRPL       PICK UP RPL ADDRESS
         ST    R0,VVD0             SAVE CALLERS PARM
         GET   RPL=(8)
         LTR   R15,R15             GET OK?                         @122
         BZ    FINDVVD3            YUP.                            @122
         C     R15,=F'8'           LOGICAL ERROR?                  @122
         BE    FINDVVD4            YES.                            @122
         ABEND 16,DUMP             OTHERWISE CRASH AND BURN.       @122
FINDVVD4 DS    0H                                                  @122
         SR    R2,R2                                               @122
         ICM   R2,1,RPLERRCD-IFGRPL(R1) GET REASON CODE.           @122
         CVD   R2,DTWORK           PACK IT.                        @122
         $EDITP FDBK,DWK3,ZZZ9     FORMAT NICELY.                  @122
        $MESSAGE 2,DATA=FDBK       SAY LOGICAL ERROR.              @122
         B     FINDVVDX            GO BACK WITH RC IN R15          @122
FINDVVD3 DS    0H                                                  @122
         AP    VVDSGETS,ONE        COUNT IT.
         L     R0,VVD0             RESTORE CALLERS PARM
         L     R6,VVDSBUF          ADDRESS OF RECORD JUST READ.
         L     R1,VVDSRPL          ADDR OF RPL
         L     R1,48(0,R1)         LENGTH OF RECORD JUST READ.
         AR    R1,R6               END OF RECORD.
         ST    R1,ENDVVD           SAVE FOR NEXT TIME.
         CLC   0(2,R6),=H'0'       ANYTHING IN THIS RECORD?        @132
         BNE   FINDVVD5            YES. LET'S DO IT.               @132
         SR    R0,R0               OOPS!                           @132
         B     FINDVVD9            GOBACK.                         @132
FINDVVD5 LA    R6,2(0,R6)          SKIP OVER PREFIX.
FINDVVD2 LTR   R0,R0               IF ZERO, ANYTHING WILL DO.
         BZ    FINDVVD9
         CLM   R0,1,2(R6)          CHECK FOR REQUESTED CELL TYPE
         BE    FINDVVD9            YES.
         CLM   R0,2,=XL1'FF'       IS THIS CELL TYPE OPTIONAL?
         BNE   FINDVVD1            NO, THEN KEEP LOOKING.
         L     R6,VVD6             RESTORE INITIAL CELL ADDRESS
         LA    R15,8               GIVE HIM THE SAD NEWS.          @134
FINDVVD9 DS    0H
         LR    R1,R13              GET SAVEAREA ADDRESS            @134
         L     R13,4(R13)          GET BACK CHAIN POINTER          @134
         L     R0,16(R13)          GET SAVEAREA LENGTH             @134
         ST    R15,16(R13)         SAVE REGISTER 15 (RETCODE)      @134
         ST    R6,44(R13)          SAVE REGISTER 15 (RETCODE)      @134
         FREEMAIN R,LV=(0),A=(1)   FREE SAVEAREA                   @134
         LM    R14,R12,12(R13)     RESTORE CALLERS REGS            @134
         L     R15,16(R13)         RESTORE RETURN CODE             @134
         BR    R14                 RETURN TO CALLER                @134
VVD9     DC    F'0'                SAVE R9 IN HERE.
VVD6     DC    F'0'                SAVE R6 IN HERE.
FINDVVDX LA    R15,8                                               @122
         B     FINDVVD9                                            @122
*        DROP  R5
         LTORG ,                                                   @134
