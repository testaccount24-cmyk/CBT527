         TITLE ' - COLLECT DATA FROM VVDS CELLS.'
* I ENTER AT THIS POINT HAVING READ IN THE 1ST RECORD IN THE CI
* CONTAINING THE DATA I WANT. FIRSTLY I HAVE TO SKIP AHEAD TO FIND
* THE VVR FOR THE OBJECT I AM LISTING (NB: THE FIRST VVR I FIND MAY
* NOT BE IT, IE. A DATA VVR IF I'M LISTING AN INDEX).
         SPACE
VVDS    $PROLOG R12
         L     R4,=V(DATASECT)
         USING DATASECT,R4
         SR    R15,R15             RESET RETURN CODE.              @122
         XC    BLKSIZE,BLKSIZE     RESET BLKSIZE
         XC    LRECL,LRECL         RESET LRECL
         XC    TRACKS,TRACKS       RESET TRACKS.
         XC    EXTENTS,EXTENTS     RESET EXTENTS
         MVI   VOLINFSW,X'00'
         MVI   VOLDSSW,X'00'
         USING VVR,R6
VVDS0    CLI   2(R6),C'Q'          Q = SECONDARY VVR
         BE    VVDS0B              YES, IS IT THE RIGHT ONE?
         CLI   2(R6),C'Z'          Z = PRIMARY VVR
         BE    VVDS0B              YES, IS IT THE RIGHT ONE?
VVDS0A   XC    VVD0,VVD0           NEXT CELL PLEASE                @134
         L     R15,=V(FINDVVDS)                                    @134
         BASR  R14,R15             SCAN VVDS                       @134
         C     R15,=F'8'           PROBLEMS?                       @122
         BNE   VVDS0               NO. IS IT A VVR?                @122
        $MESSAGE 3,DATA=COMPNAME                                   @122
         B     VVDS98              RETURN WITH RC=8                @122
VVDS0B   SR    R1,R1
         ICM   R1,1,VVRCMPNL       PICK UP NAME LENGTH.
         BCTR  R1,0
         BCTR  R1,0
         B     VVDS0C
         CLC   COMPNAME(0),VVRCMPNM  (EXECUTED)
VVDS0C   EX    R1,*-6              COMPARE VVR NAME TO BCS NAME.
         BNE   VVDS0A              NO, MUST BE A RELATED COMPONENT.
VVDS0D   CLI   2(R6),C'Q'          Q = SECONDARY VVR
         BE    VVDS1               YES, IS IT THE RIGHT ONE?
         CLI   2(R6),C'Z'          Z = PRIMARY VVR
         BE    VVDS2               YES, IS IT THE RIGHT ONE?
         B     VVDS99              SHOULD NEVER HAPPEN!
VVDS1    MVC   VVD0,=X'0000FF23'   VOLUME INFORMATION CELL         @134
         L     R15,=V(FINDVVDS)                                    @134
         BASR  R14,R15             SCAN VVDS                       @134
         LTR   R15,R15             WAS THERE A VOLINFO CELL?       @134
         BNZ   VVDS2               NO.                             @134
         MVI   VOLINFSW,X'FF'
         USING VVRVOLIN,R6
         SR    R1,R1               COUNT OF TRACKS
         SR    R8,R8
         ICM   R8,1,VVRNOEXT       GET NUMBER OF EXTENTS
         ST    R8,EXTENTS
         LA    R6,40(0,R6)         SKIP AHEAD TO EXTENT DESCR.
         USING VVRXTENT,R6
VVDS1B   AH    R1,VVRXNTRK         COUNT TRACKS THIS EXTENT
         LA    R6,20(0,R6)         SKIP TO NEXT EXTENT DESCR.
         BCT   R8,VVDS1B
         ST    R1,TRACKS           SAVE ALLOCATED TRACKS.
         CLI   VOLDSSW,X'FF'       GOT DATASET INFORMATION CELL YET?
         BNE   VVDS2               NO, GO GET IT.
         B     VVDS99
         DROP  R6
VVDS2    MVC   VVD0,=X'00000021'   DATASET INFORMATION CELL.       @134
         L     R15,=V(FINDVVDS)                                    @134
         BASR  R14,R15             SCAN VVDS                       @134
         MVI   VOLDSSW,X'FF'
         USING VVRDSINF,R6
         MVI   SHREG,C'1'
         MVI   SHSYS,C'1'
         TM    VVRATTR2,VVRA2REG
         BM    VVDS3A
         BNO   VVDS3
         MVI   SHREG,C'4'
         B     VVDS3
VVDS3A   EQU   *
         TM    VVRATTR2,B'01000000'
         BNO   VVDS3B
         MVI   SHREG,C'2'
         B     VVDS3
VVDS3B   EQU   *
         MVI   SHREG,C'3'
VVDS3    EQU   *
         TM    VVRATTR2,VVRA2SYS
         BM    VVDS4A
         BNO   VVDS4
         MVI   SHSYS,C'4'
         B     VVDS4
VVDS4A   EQU   *
         TM    VVRATTR2,B'00010000'
         BNO   VVDS4B
         MVI   SHSYS,C'2'
         B     VVDS4
VVDS4B   EQU   *
         MVI   SHSYS,C'3'
VVDS4    EQU   *
         MVC   LRECL,VVRLRECL      SAVE AVERAGE LRECL
         ICM   R1,7,VVRSECSP       PICK UP SECONDARY SPACE.        @130
         CVD   R1,WORK             CONVERT TO DECIMAL.             @130
         $EDITP  L1ALSEC,                                          @130X
               DWK3,ZZZZ9          FORMAT IT.                      @130
         ICM   R1,7,VVRPRISP       PICK UP PRIMARY SPACE.          @130
         CVD   R1,WORK             CONVERT TO DECIMAL.             @130
         $EDITP  L1ALPRM,                                          @130X
               DWK3,ZZZZ9          FORMAT IT.                      @130
         TM    VVRSPCFG,DS1CYL     CYLINDER ALLOCATION
         BM    VVDS1Z
         MVC   L1ALTYP,=C'CYL'
         B     VVDS1C
VVDS1Z   TM    VVRSPCFG,DS1TRK     TRACK ALLOCATION
         BZ    VVDS1Y
         MVC   L1ALTYP,=C'TRK'
         B     VVDS1C
VVDS1Y   TM    VVRSPCFG,DS1AVR     BLOCK ALLOCATION
         BZ    VVDS1Y
         MVC   L1ALTYP,=C'BLK'
VVDS1C   SR    R1,R1               CLEAR REGISTER                  @130
         ICM   R0,15,VVRDSHU       GET HIGH USED RBA.              @130
         SRDA  R0,32               READY FOR MULTIPLY              @130
         M     R0,=F'100'          TIMES 100.                      @130
*        LR    R0,R1                                               @130
*        SRDA  R0,32               READY FOR DIVIDE.               @130
         ICM   R2,15,VVRDSHA       GET HIGH ALLOC RBA              @130
         DR    R0,R2               (USED*100)/ALLOC                @130
         CVD   R1,WORK             CONVERT TO DECIMAL              @130
         $EDITP  L1USED,                                           @130X
               DWK2,ZZ9            FORMATTED.                      @130
         DROP  R6
         MVC   VVD0,=X'0000FF60'   NEED AN AMDSB NEXT              @134
         L     R15,=V(FINDVVDS)                                    @134
         BASR  R14,R15             SCAN VVDS                       @134
         LTR   R15,R15             WAS THERE AN AMDSB?             @134
         BNZ   VVDS1               NO. TRY A VOL INFO CELL.        @134
         USING VVRAMDCL,R6
         SR    R1,R1               CLEAR REGISTER                  @130
         ICM   R1,1,VVRAMPCA       PICK UP CA FREESPACE %          @130
         CVD   R1,WORK             CONVERT TO DECIMAL              @130
         $EDITP  L1CAFS,                                           @130X
               DWK2,Z9             AND FORMAT.                     @130
         SR    R1,R1               CLEAR REGISTER                  @130
         ICM   R1,1,VVRAMPCI       PICK UP CI FREESPACE %          @130
         CVD   R1,WORK             CONVERT TO DECIMAL              @130
         $EDITP  L1CIFS,                                           @130X
               DWK2,Z9             AND FORMAT.                     @130
         ICM   R1,15,VVRAMCAS      PICK UP CA SPLITS.              @130
         CVD   R1,WORK             CONVERT TO DECIMAL              @130
         $EDITP  L1CASP,                                           @130X
               DWK2,ZZZ9           FORMAT IT NICELY.               @130
         ICM   R1,15,VVRAMCIS      PICK UP CI SPLITS.              @130
         CVD   R1,WORK             CONVERT TO DECIMAL              @130
         $EDITP  L1CISP,                                           @130X
               DWK2,ZZZ9           FORMAT IT NICELY.               @130
         MVC   L1CRSHR,SHREG
         MVC   L1CSSHR,SHSYS
         SR    R1,R1               CLEAR REGISTER                  @130
         ICM   R1,3,VVRAMRKP       PICK UP MAX KEY POSITION.       @130
         CVD   R1,WORK             CONVERT TO DECIMAL              @130
         $EDITP  L1KPOS,                                           @130X
               DWK2,ZZ9            FORMAT IT NICELY.               @130
         SR    R1,R1               CLEAR REGISTER                  @130
         ICM   R1,3,VVRAMKNL       PICK UP KEY LENGTH.             @130
         CVD   R1,WORK             CONVERT TO DECIMAL              @130
         $EDITP  L1KLEN,                                           @130X
               DWK2,ZZ9            FORMAT IT NICELY.               @130
*        MVC   EXTENTS,VVRAMNXT    GET NUMBER OF EXTENTS.
         ICM   R1,15,VVRAMLCL      PICK UP MAX RECORD LENGTH.      @130
         CVD   R1,WORK             CONVERT TO DECIMAL              @130
         $EDITP  L1MRECL,                                          @130X
               DWK3,ZZZZZ9         FORMAT IT NICELY.               @130
         ICM   R1,15,VVRAMNLR      PICK UP NUMBER OF RECORDS.      @130
         CVD   R1,WORK             CONVERT TO DECIMAL              @130
         $EDITP  L1NOREC,                                          @130X
               DWK4,ZZZZZZZ9       FORMAT IT NICELY.               @130
         MVC   BLKSIZE,VVRAMCIV    SAVE BLOCK SIZE.
         CLI   VOLINFSW,X'FF'      GOT VOLUME INFORMATION CELL YET?
         BNE   VVDS1               LOOK FOR VOLUME INFORMATION CELL
         DROP  R6
VVDS99   DS    0H
        $EPILOG ,                                                  @134
VVDSR9   DC    F'0'
VVDS98   LA    R15,8               RC=8 FOR CALLER.                @122
         B     VVDS99                                              @122
         LTORG ,                                                   @134
